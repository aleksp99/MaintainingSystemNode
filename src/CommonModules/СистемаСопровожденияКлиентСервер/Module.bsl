#Область ПрограммныйИнтерфейс

Функция ЗначениеИзСтроки(Знач Строка) Экспорт
	
	Если Не (СтрНачинаетсяС(Строка, """") И СтрЗаканчиваетсяНа(Строка, """")) Тогда 
		Возврат Строка;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(СтрЗаменить(Сред(Строка, 2, СтрДлина(Строка)-2), """""", """"));
	Для Номер = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		Строка = ТекстовыйДокумент.ПолучитьСтроку(Номер);
		Если СтрНачинаетсяС(СокрЛ(Строка), "|") Тогда
			Позиция = СтрНайти(Строка, "|");
			ТекстовыйДокумент.ЗаменитьСтроку(Номер, Лев(Строка, Позиция-1) + Сред(Строка, Позиция+1));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТекстовыйДокумент.ПолучитьТекст();
	
КонецФункции

Функция ЗначениеТекстовогоФайла(ПолноеИмя) Экспорт

#Если Не ВебКлиент Тогда

	ЧтениеДанных = Новый ЧтениеДанных(ПолноеИмя);
	Буфер = ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных();
	ЧтениеДанных.Закрыть();
	ЧтениеТекста = Новый ЧтениеТекста(Новый ПотокВПамяти(Буфер));
	Текст = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Возврат Текст;

#Иначе
		ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на Веб-клиенте.'"); // Для модуля объекта или набора записей
#КонецЕсли

КонецФункции

#КонецОбласти