#Область ПрограммныйИнтерфейс

Функция Формат1СВМассив(Текст) Экспорт
	
	RegExp = Новый COMОбъект("VBScript.RegExp");
	RegExp.Global = Истина;
	RegExp.Multiline = Истина;
	RegExp.IgnoreCase = Истина;
	RegExp.Pattern = "({)|(""(?:(?:"""")|[^""])*"")|([A-F0-9]{8}(?:-[A-F0-9]{4}){3}-[A-F0-9]{12})|(\d+)|(})";
	Возврат Формат1СВМассивРекурсивно(RegExp.Execute(Текст));
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Формат1СВМассивРекурсивно(Вхождения, Начало = 0)
	
	Результат = Новый Массив;
	Для Номер = Начало По Вхождения.Count-1 Цикл 
		
		Вхождение = Вхождения.Item(Номер);
		Если Не Вхождение.SubMatches(0) = Неопределено Тогда // {
			Если Не Результат.Количество() И Номер = Начало Тогда 
				Продолжить;
			КонецЕсли;
			Результат.Добавить(Формат1СВМассивРекурсивно(Вхождения, Номер));
			
		ИначеЕсли Не Вхождение.SubMatches(1) = Неопределено Тогда // ""
			Результат.Добавить(СистемаСопровожденияКлиентСервер.ЗначениеИзСтроки(Вхождение.Value));
			
		ИначеЕсли Не Вхождение.SubMatches(2) = Неопределено Тогда // 00000000-0000-0000-0000-000000000000
			Результат.Добавить(Новый УникальныйИдентификатор(Вхождение.Value));
			
		ИначеЕсли Не Вхождение.SubMatches(3) = Неопределено Тогда // 0-9
			Значение = СтроковыеФункцииКлиентСервер.СтрокаВДату(Вхождение.SubMatches(3), ЧастиДаты.ДатаВремя);
			Если Формат(Значение, "ДФ=ггггММддЧЧммсс; ДП=00010101000000") = Вхождение.SubMatches(3) Тогда
				Результат.Добавить(Значение);
			Иначе
				Результат.Добавить(Число(Вхождение.Value));
			КонецЕсли;
			
		ИначеЕсли Не Вхождение.SubMatches(4) = Неопределено Тогда // }
			Начало = Номер;
			Возврат Результат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#КонецОбласти